apply plugin: 'com.android.library'
apply plugin: 'com.google.devtools.ksp'
apply plugin: 'kotlin-android'

android {
    compileSdkVersion sdkCompileVersion

    defaultConfig {
        minSdkVersion sdkMinVersion
        targetSdkVersion sdkTargetVersion

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        // The schemas directory contains a schema file for each version of the Room database.
        // This is required to enable Room auto migrations.
        // See https://developer.android.com/reference/kotlin/androidx/room/AutoMigration.
        ksp {
            arg("room.schemaLocation", "$projectDir/schemas")
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    sourceSets {
        androidTest.java.srcDirs += "src/test-common/java"
        test.java.srcDirs += "src/test-common/java"
        // Room Database Tests
        androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
    }

    packagingOptions {
        resources.excludes.add("META-INF/*")
    }

    namespace "com.owncloud.android.data"
}

dependencies {
    implementation project(":owncloudDomain")

    // Owncloud Android Library
    api project(":owncloud-android-library:owncloudComLibrary")

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$orgJetbrainsKotlin"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$orgJetbrainsKotlinx"

    // Room
    implementation "androidx.room:room-ktx:$androidxRoom"
    ksp "androidx.room:room-compiler:$androidxRoom"

    implementation("com.squareup.moshi:moshi-kotlin:$comSquareupMoshi")
    ksp "com.squareup.moshi:moshi-kotlin-codegen:$comSquareupMoshi"

    // Dependencies for unit tests
    testImplementation project(":owncloudTestUtil")
    testImplementation "junit:junit:$junitVersion"
    testImplementation "androidx.arch.core:core-testing:$androidxArchCore"
    testImplementation "io.mockk:mockk:$ioMockk"

    // Dependencies for instrumented tests
    androidTestImplementation project(":owncloudTestUtil")
    androidTestImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$orgJetbrainsKotlinx"
    androidTestImplementation "androidx.test:runner:$androidxTest"
    androidTestImplementation "androidx.test.espresso:espresso-core:$androidxTestEspresso"
    androidTestImplementation "androidx.test.ext:junit:$androidxTestExt"
    androidTestImplementation "androidx.arch.core:core-testing:$androidxArchCore"
    androidTestImplementation "androidx.room:room-testing:$androidxRoom"
    androidTestImplementation("io.mockk:mockk-android:$ioMockk") {
        exclude module: "objenesis"
    }
}
